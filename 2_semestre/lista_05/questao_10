função Junta_Seq(nome1, nome2, nome3: string): lógico
var
    f1, f2, f3: sequencia de real
    num1, num2: real
    acabou1, acabou2: lógico
    procedimento Atualiza(ref f: sequencia de real, ref num: real, ref acabou: logico)
    inicio
        se nao fds(f) entao
            leia(f, num)
            acabou <- FALSO
        senao
            acabou <- VERDADEIRO
        fimse
    fim procedimento
inicio
    se nao abre(f1, nome1) entao
        retorne FALSO
    fim se
    se nao abre(f2, nome2) entao
        fecha(f1)
        retorne FALSO
    fim se
    se nao abre(f3, nome3) entao
        fecha(f1)
        fecha(f2)
        retorne FALSO
    fim se
    Atualiza(f1, num1, acabou1)
    Atualiza(f2, num2, acabou2)
    enquanto nao acabou1 ou nao acabou2 faça
        se acabou1 ou (nao acabou2 e num1 > num2) entao
            escreva(f3, num2)
            Atualiza(f2, num2, acabou2)
        senao
            escreva(f3, num1)
            Atualiza(f1, num1, acabou1)
        fim se
    fim enquanto
    fecha(f1)
    fecha(f2)
    fecha(f3)
    retorne VERDADEIRO
fim função
                                